apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'war'

configurations.all {
	resolutionStrategy.cacheChangingModulesFor 14, 'days'
}

repositories {
	mavenCentral()
	mavenRepo(url: 'http://download.eclipse.org/jgit/maven')
	flatDir { dirs 'lib' }
}

dependencies {
	compile 'org.eclipse.jgit:org.eclipse.jgit:1.2.+'
	compile 'net.iharder:base64:2.3.8'
	[
		'jersey-core',
		'jersey-server',
		'jersey-servlet'
	].each { compile "com.sun.jersey:" + it + ":1.11" }

	compile 'com.google.guava:guava:11.+'
	compile 'org.codehaus.jackson:jackson-jaxrs:1.9.+'
	compile 'com.github.spullara.mustache.java:builder:0.6+'

	providedCompile 'org.mortbay.jetty:servlet-api:3.0+'

	testCompile 'org.eclipse.jetty:jetty-server:8.0.+'
	testCompile 'org.eclipse.jetty:jetty-servlet:8.0.+'
	testCompile ('org.eclipse.jetty:jetty-plus:8.0.+') {
		exclude module: 'geronimo-jta_1.1_spec'
		exclude module: 'jetty-jndi'
	}

	testCompile 'junit:junit:4.+'
	testCompile 'com.sun.jersey:jersey-client:1.11'
	testCompile ('com.sun.jersey.jersey-test-framework:jersey-test-framework-core:1.11') { exclude group: 'javax.servlet' }
}

sourceSets {
	main {
		java.srcDir 'src/java'
		resources.srcDir 'src/java'
	}
	bootstrap { java.srcDir "src/bootstrap" }
	test {
		java.srcDir 'test/java'
		resources.srcDir 'test/java'
	}
}

sourceCompatibility = 1.7
targetCompatibility = 1.7
webAppDirName = "src/webapp"

tasks.withType(AbstractCompile).each { it.options.encoding = "UTF-8" }

eclipse {
	project { name = 'koshinuke.java' }
	classpath {
		containers = [
			'org.eclipse.jdt.launching.JRE_CONTAINER/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/JavaSE-1.7'
		]
		defaultOutputDir = file('bin')
		file {
			whenMerged {
				it.entries.find { it.path == 'src/java'}?.output = 'src/webapp/WEB-INF/classes'
			}
		}
	}
}

buildscript {
	repositories {
		mavenCentral()
		mavenRepo(url: 'http://download.eclipse.org/jgit/maven')
		flatDir { dirs 'lib' }
	}
	dependencies {
		classpath 'org.eclipse.jgit:org.eclipse.jgit:1.2.+'
		classpath 'org.eclipse.jetty:jetty-http:8.0.+'
		classpath 'com.google.guava:guava:11.+'
	}
}

import java.nio.file.*
import org.eclipse.jgit.api.*
import org.eclipse.jgit.lib.*
import org.eclipse.jgit.storage.file.FileRepositoryBuilder

task cloneWeb(dependsOn: clean) << {
	def dir = file("$buildDir/cloneweb/")

	FileSystem fs = FileSystems.getDefault()
	Path cp = fs.getPath(koshinuke_path, ".git")

	CloneCommand clone = Git.cloneRepository();
	clone.setURI cp.toUri().toString()
	clone.setDirectory dir
	clone.setBranch "HEAD"
	clone.setProgressMonitor new TextProgressMonitor()
	clone.call()

	project.copy {
		into 'src/webapp/WEB-INF/mustache/'
		from(dir) { include '*.html' }
	}
	project.copy {
		into 'src/webapp/'
		from(dir) { include "static/**" }
		exclude "**/.git*"
	}
}

task sym << {
	FileSystem fs = FileSystems.getDefault()
	Path cp = fs.getPath(closure_path)
	Path tg = fs.getPath("src/webapp/static/closure-library")
	Files.createSymbolicLink(tg, cp);
}

task passwd {
	description = "login.properties Utility."
	doLast {
		println "taichi: " + org.eclipse.jetty.http.security.Credential.MD5.digest("taichipass")
		println "lanius: " + org.eclipse.jetty.http.security.Credential.Crypt.crypt("lanius", "laniuspass")
	}
}